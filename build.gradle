import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.codehaus.griffon:gradle-griffon-plugin:2.0.0'
        classpath 'org.codehaus.griffon:gradle-griffon-build-plugin:2.0.0'
        // classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
        classpath 'org.ajoberstar:gradle-git:0.11.1'
        classpath 'org.kordamp.gradle:stats-gradle-plugin:0.1.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5'
    }
}

apply plugin: 'base'
apply plugin: 'idea'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'build-dashboard'
apply from: 'gradle/idea.gradle'
apply plugin: 'org.codehaus.griffon.griffon-build'
apply from: 'gradle/bom.gradle'
apply plugin: 'org.ajoberstar.github-pages'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'idea'
    apply plugin: 'com.github.ben-manes.versions'

    repositories {
        jcenter()
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.7.2.201409121644'
}

ext.jacocoProjects = []

subprojects { subproj ->
    plugins.withType(JavaPlugin) {
        configurations {
            compileOnly
            testCompileOnly
        }

        subproj.tasks.withType(JavaCompile) {
            sourceCompatibility = rootProject.sourceCompatibility
            targetCompatibility = rootProject.targetCompatibility
        }

        sourceSets {
            main {
                compileClasspath += [configurations.compileOnly]
            }
            test {
                compileClasspath += [configurations.testCompileOnly]
            }
        }

        javadoc {
            classpath += [configurations.compileOnly]
        }

        idea {
            module {
                scopes.PROVIDED.plus += [configurations.compileOnly]
                scopes.PROVIDED.plus += [configurations.testCompileOnly]
            }
        }

        dependencies {
            testCompile 'junit:junit:4.11'
            testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
            testCompile 'org.slf4j:slf4j-simple:1.7.7'
        }

        task sourceJar(type: Jar) {
            group 'Build'
            description 'An archive of the source code'
            classifier 'sources'
            from sourceSets.main.allSource
        }

        tasks.withType(AbstractCompile) {
            if (rootProject.hasProperty('lint') && rootProject.lint.toBoolean()) {
                options.compilerArgs = [
                    '-Xlint:all', '-Xlint:deprecation', '-Xlint:unchecked'
                ]
            }
        }

        subproj.apply from: rootProject.file('gradle/publishing.gradle')
        subproj.apply from: rootProject.file('gradle/code-quality.gradle')

        if (subproj.publishJars.toBoolean()) {
            subproj.apply from: rootProject.file('gradle/docs.gradle')
        }

        subproj.test.testLogging { exceptionFormat 'full' }
    }
}

evaluationDependsOnChildren()

if (!project.hasProperty('githubUsername')) ext.githubUsername = ''
if (!project.hasProperty('githubPassword')) ext.githubPassword = ''

githubPages {
    repoUri = project.projectVcsUrl
    pages {
        from project(":${pluginBaseName}-guide").guide.outputs.files
    }

    credentials {
        username = githubUsername
        password = githubPassword
    }
}

publishGhPages.dependsOn(project(":${pluginBaseName}-guide").guide)

coveralls {
    sourceDirs = files(jacocoProjects.sourceSets.main.allSource.srcDirs).files.absolutePath
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = jacocoProjects.test
    additionalSourceDirs = files(jacocoProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(jacocoProjects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(jacocoProjects.sourceSets.main.output)
    executionData = files(jacocoProjects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
        html.destination = "${buildDir}/reports/jacoco/html"
        xml.destination = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}
